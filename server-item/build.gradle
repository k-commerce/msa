buildscript {
    ext {
        mapstructVersion = "1.5.3.Final"
        querydslVersion = "5.0.0"
        springCloudVersion = "2021.0.1"
    }
}

plugins {
    id "java"
    id "org.springframework.boot" version "2.7.9"
    id "io.spring.dependency-management" version "1.0.15.RELEASE"
    // querydsl
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = "com.kcommerce"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    // querydsl
    querydsl.extendsFrom compileClasspath
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    compileOnly "org.projectlombok:lombok"
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    runtimeOnly "com.mysql:mysql-connector-j"
    annotationProcessor "org.projectlombok:lombok"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    // mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // querydsl
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}"

    // eureka-client
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
}

tasks.named("test") {
    useJUnitPlatform()
}

// querydsl
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

// spring cloud
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
