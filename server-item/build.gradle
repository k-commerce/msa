buildscript {
    ext {
        mapstructVersion = "1.5.3.Final"
        springdocVersion = "1.6.14"
        springCloudVersion = "2021.0.1"
        querydslVersion = "5.0.0"
    }
}

plugins {
    id "java"
    id "org.springframework.boot" version "2.7.9"
    id "io.spring.dependency-management" version "1.0.15.RELEASE"
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = "com.kcommerce"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // spring-boot
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    // lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    // mysql
    runtimeOnly "com.mysql:mysql-connector-j"

    // mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    // springdoc
    implementation "org.springdoc:springdoc-openapi-webmvc-core:${springdocVersion}"

    // spring-cloud
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"

    // querydsl
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}"
}

tasks.named("test") {
    useJUnitPlatform()
}

// querydsl
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}
